/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

buildscript {
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.15.0"
        classpath 'gradle.plugin.org.jetbrains.gradle.plugin.idea-ext:gradle-idea-ext:1.0'
        classpath 'name.remal:gradle-plugins:1.3.1'
    }
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

allprojects {
    group = "name.remal.gradle-plugins.${rootProject.name}"
    //version = '2-SNAPSHOT'
    version = '2'
}

apply from: 'gradle/properties.gradle'
apply from: 'gradle/ci.gradle'
apply from: 'gradle/dependencies.gradle'
apply from: 'gradle/java.gradle'
apply from: 'gradle/groovy.gradle'
apply from: 'gradle/checkstyle.gradle'
apply from: 'gradle/gradle-plugin.gradle'
apply from: 'gradle/base-package.gradle'
apply from: 'gradle/publish-java.gradle'
apply from: 'gradle/publish-gradle-plugin.gradle'
apply from: 'gradle/signing.gradle'
apply from: 'gradle/ide.gradle'
apply from: 'gradle/renovate.gradle'

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'

configurations.optional.extendsFrom(configurations.correspondingKotlinPlugin)

dependencies {
}

gradlePlugin {
    plugins {
        'name.remal.test-source-sets' {
            id = 'name.remal.test-source-sets'
            implementationClass = 'name.remal.gradleplugins.testsourcesets.TestSourceSetsPlugin'
            displayName = 'A plugin that creates `testSourceSets` extension'
            description = property('repository-description')
        }
        'name.remal.integration-tests' {
            id = 'name.remal.integration-tests'
            implementationClass = 'name.remal.gradleplugins.testsourcesets.IntegrationTestsPlugin'
            displayName = 'A plugin that creates `integration` source set for integration tests'
            description = displayName
        }
    }
}

/* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * */

apply plugin: 'maven-publish'

allprojects {
    pluginManager.withPlugin('maven-publish') {
        publishing.publications.withType(MavenPublication) {
            pom {
                developers {
                    developer {
                        name = 'Semyon Levin'
                        email = 'levin.semen@gmail.com'
                        id = email
                    }
                }
            }
        }
    }
}
